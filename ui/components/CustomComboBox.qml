import QtQuick
import QtQuick.Controls

ComboBox {
    id: control
    width: 200
    height: 40
    hoverEnabled: true
    property int optionHeight: 35
    property int popupPadding: 6

    contentItem: Text {
        text: control.displayText !== "" ? control.displayText : "Select"
        anchors.fill: parent
        font.pixelSize: Typography.bigFontSize
        font.family: Typography.fontBold
        horizontalAlignment: Text.AlignHCenter
        verticalAlignment: Text.AlignVCenter
        color: mouseArea.containsMouse ? Theme.hoverTextColor : Theme.textColor

        Behavior on color { ColorAnimation { duration: 150 } }
    }

    indicator: Text {
        text: "â–¾"
        color: contentItem.color
        font.pixelSize: Typography.iconFontSize
        anchors.right: parent.right
        anchors.rightMargin: 10
        anchors.verticalCenter: parent.verticalCenter
    }

    background: Rectangle {
        anchors.fill: parent
        radius: 6
        border.color: Theme.borderColor
        border.width: 3
        color: mouseArea.containsMouse ? Theme.hoverBackgroundColor : Theme.backgroundColor

        Behavior on color { ColorAnimation { duration: 150 } }

        MouseArea {
            id: mouseArea
            anchors.fill: parent
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            onClicked: control.open()
        }
    }

    popup: Popup {
        width: control.width
        implicitHeight: control.count * control.optionHeight + control.popupPadding * 2

        padding: 0
        topPadding: 0
        bottomPadding: 0
        leftPadding: 0
        rightPadding: 0

        background: Rectangle {
            radius: 6
            color: Theme.backgroundColor
            border.color: Theme.borderColor
            border.width: 3
        }

        Column {
            anchors.fill: parent
            anchors.margins: control.popupPadding
            spacing: 0

            Repeater {
                model: control.model

                delegate: Rectangle {
                    width: parent.width
                    height: control.optionHeight
                    radius: 6
                    property bool hovered: false
                    color: hovered ? Theme.themeColor : "transparent"

                    MouseArea {
                        anchors.fill: parent
                        hoverEnabled: true
                        cursorShape: Qt.PointingHandCursor
                        onEntered: parent.hovered = true
                        onExited: parent.hovered = false
                        onClicked: {
                            control.currentIndex = index
                            control.popup.close()
                        }
                    }

                    Text {
                        anchors.fill: parent
                        text: control.textRole ? model[control.textRole] : modelData
                        font.pixelSize: Typography.bigFontSize
                        font.family: Typography.fontBold
                        horizontalAlignment: Text.AlignHCenter
                        verticalAlignment: Text.AlignVCenter
                        color: hovered ? Theme.hoverTextColor : Theme.textColor
                    }
                }
            }
        }
    }
}
